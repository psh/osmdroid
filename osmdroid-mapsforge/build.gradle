description = "An Android library to display OpenStreetMap views using Mapsforge. May use non ASF licensed dependencies"
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.kotlin.android'

group = project.property("pom.groupId")
version =  project.property("pom.version")

android {
    namespace "org.osmdroid.mapsforge"
    compileSdk findProperty('android.compileSdkVersion').toInteger()

    defaultConfig {
        // mapsforge 0.8.0 has minSdk 10
        minSdkVersion Integer.parseInt(project.property('android.minSdkVersion'))
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    testImplementation libs.junit.vintage.engine

    //Mapsforge rendering and database support, which is LGPL
    api libs.mapsforge.map.android
    api libs.mapsforge.map
    api libs.mapsforge.themes

    //osmdroid which is ASF licensed
    api  project(':osmdroid-android' )
}



tasks.register('androidSourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
//    archives packageReleaseJar
    archives androidSourcesJar

}

afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                from components.release
                groupId project.property("pom.groupId")
                artifactId project.name
                version = project.property("pom.version")
                pom {
                    description =  project.description
                    url = project.property("pom.url")

                    //scm, organization and developers are injected via other mechanisms

                    licenses {
                        license {
                            name=project.property("pom.licenses.license.0.name");
                            url=project.property("pom.licenses.license.0.url");
                            distribution==project.property("pom.licenses.license.0.distribution");
                        }

                    }
                }
            }
        }
    }


}

//apply from: "$rootDir/gradle/javadoc-aar.gradle"
